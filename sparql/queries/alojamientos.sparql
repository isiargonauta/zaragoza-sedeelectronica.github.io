#Alojamientos cercanos a la Bas√≠lica del Pilar
PREFIX tur:<http://vocab.linkeddata.es/kos/turismo/>
PREFIX mon:<http://www.zaragoza.es/api/recurso/turismo/monumento/>
SELECT DISTINCT ?rest ?restName (bif:round(bif:st_distance(bif:st_point (?lngM, ?latM),
		bif:st_point (?lngR, ?latR))) AS ?distancia)  
WHERE {
	?monum geo:geometry ?geoMonum.
	FILTER(?monum=mon:32).
	?geoMonum geo:lat ?latM;
		geo:long ?lngM.
	?rest a tur:alojamiento;
		rdfs:label ?restName;
		geo:geometry ?geoRest.
	?geoRest geo:lat ?latR;
		geo:long ?lngR.
	FILTER(bif:st_distance(bif:st_point (?lngM, ?latM),bif:st_point (?lngR, ?latR))<1).
}
ORDER BY ASC(?distancia)

#Tipos de alojamientos	
PREFIX tur:<http://vocab.linkeddata.es/kos/turismo/>
PREFIX alo:<http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento>
SELECT DISTINCT ?categoria 
WHERE {
        ?uriCont a tur:alojamiento;
        	alo:categoria ?categoria.
}

#Hoteles de tres estrellas	
PREFIX alo: <http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento#>
SELECT DISTINCT ?rest ?restName 
WHERE {
	?rest a <http://vocab.linkeddata.es/kos/turismo/alojamiento>;
		rdfs:label ?restName;
		alo:categoria ?categoria.
	FILTER(regex(?categoria, "Hotel 3 estrellas")).
}


#Detalle del alojamiento 16	
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX v: <http://www.w3.org/2006/vcard/ns#>
PREFIX lug: <http://vocab.linkeddata.es/datosabiertos/def/turismo/lugar#>
PREFIX alo: <http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento#>
PREFIX s: <http://schema.org/>
SELECT distinct ?uri ?title ?latitud ?longitud ?modified ?tel ?fax ?comment ?accesibilidad ?numCamas 
		?numHabitaciones ?streetAdr ?addressLocality ?postCode ?email ?categoria ?logo ?photo ?url
WHERE { 
	?uri a <http://vocab.linkeddata.es/kos/turismo/alojamiento>.
	OPTIONAL {?uri rdfs:label  ?title.}
	OPTIONAL {?uri dcterms:modified  ?modified.}
	OPTIONAL {?uri rdfs:comment  ?comment.}
	OPTIONAL {?uri s:addressLocality  ?addressLocality.}
	OPTIONAL {?uri locn:postCode  ?postCode.}
	OPTIONAL {?uri s:email  ?email.}
	OPTIONAL {?uri v:logo  ?logo.}
	OPTIONAL {?uri v:photo  ?photo.}
	OPTIONAL {?uri v:url  ?url.}
	OPTIONAL {?uri v:street-adr  ?streetAdr.}
	OPTIONAL {?uri alo:numCamas  ?numCamas.}
	OPTIONAL {?uri alo:numHabitaciones  ?numHabitaciones.}
	OPTIONAL {?uri lug:accesibilidad  ?accesibilidad.}
	OPTIONAL {?uri alo:categoria ?categoria.}
	OPTIONAL {?uri s:telephone ?tel.}
	OPTIONAL {?uri s:faxNumber ?fax.}
	OPTIONAL {
		?uri geo:geometry ?geo.
		?geo geo:lat ?latitud.
		?geo geo:long ?longitud
	}.
	?uri dcterms:identifier "16".
}