#Alojamientos cercanos a la Bas√≠lica del Pilar
PREFIX tur:<http://vocab.linkeddata.es/kos/turismo/>
PREFIX mon:<http://www.zaragoza.es/api/recurso/turismo/monumento/>
SELECT DISTINCT ?rest ?restName (bif:round(bif:st_distance(bif:st_point (?lngM, ?latM),bif:st_point (?lngR, ?latR)))AS ?distancia)  
WHERE {
	?monum geo:geometry ?geoMonum.
	FILTER(?monum=mon:32).
	?geoMonum geo:lat ?latM;
		geo:long ?lngM.
	?rest a tur:alojamiento;
		rdfs:label ?restName;
		geo:geometry ?geoRest.
	?geoRest geo:lat ?latR;
		geo:long ?lngR.
	FILTER(bif:st_distance(bif:st_point (?lngM, ?latM),bif:st_point (?lngR, ?latR))<1).
}
ORDER BY ASC(?distancia)

#Tipos de alojamientos	
PREFIX tur:<http://vocab.linkeddata.es/kos/turismo/>
PREFIX alo:<http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento>
SELECT DISTINCT ?categoria 
WHERE {
        ?uriCont a tur:alojamiento;
        	alo:categoria ?categoria.
}

#Hoteles de tres estrellas	
SELECT DISTINCT ?rest ?restName 
WHERE {
	?rest a <http://vocab.linkeddata.es/kos/turismo/alojamiento>;
		rdfs:label ?restName;
		<http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento#categoria> ?categoria.
	FILTER(regex(?categoria, "Hotel 3 estrellas")).
}


#Detalle del alojamiento 16	
PREFIX locn: <http://www.w3.org/ns/locn#>
SELECT distinct ?uri ?title ?latitud ?longitud ?modified ?tel ?fax ?comment ?accesibilidad ?numCamas ?numHabitaciones ?streetAdr ?addressLocality ?postCode ?email ?socioHoreca ?categoria ?locality ?logo ?photo ?url
WHERE { 
	?uri a <http://vocab.linkeddata.es/kos/turismo/alojamiento>.
	OPTIONAL {?uri rdfs:label  ?title.}
	OPTIONAL {?uri dcterms:modified  ?modified.}
	OPTIONAL {?uri rdfs:comment  ?comment.}
	OPTIONAL {?uri s:addressLocality  ?addressLocality.}
	OPTIONAL {?uri locn:postCode  ?postCode.}
	OPTIONAL {?uri <http://schema.org/email>  ?email.}
	OPTIONAL {?uri <http://www.w3.org/2006/vcard/ns#logo>  ?logo.}
	OPTIONAL {?uri <http://www.w3.org/2006/vcard/ns#photo>  ?photo.}
	OPTIONAL {?uri <http://www.w3.org/2006/vcard/ns#url>  ?url.}
	OPTIONAL {?uri <http://www.w3.org/2006/vcard/ns#street-adr>  ?streetAdr.}
	OPTIONAL {?uri <http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento#numCamas>  ?numCamas.}
	OPTIONAL {?uri <http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento#numHabitaciones>  ?numHabitaciones.}
	OPTIONAL {?uri <http://vocab.linkeddata.es/datosabiertos/def/turismo/lugar#accesibilidad>  ?accesibilidad.}
	OPTIONAL {?uri <http://www.zaragoza.es/api/def/turismo/lugar#socioHoreca>  ?socioHoreca.}
	OPTIONAL {?uri <http://vocab.linkeddata.es/datosabiertos/def/turismo/alojamiento#categoria> ?categoria.}
	OPTIONAL {?uri <http://schema.org/telephone> ?tel.}
	OPTIONAL {?uri <http://schema.org/faxNumber> ?fax.}
	OPTIONAL {
		?uri geo:geometry ?geo.
		?geo geo:lat ?latitud.
		?geo geo:long ?longitud
	}.
	?uri dcterms:identifier "16".
}