# Todos los organismos con su URI y nombre, y la URI de su organismo padre
PREFIX orgzar: <http://vocab.linkeddata.es/kos/sector-publico/> 
PREFIX w3org: <http://www.w3.org/ns/org#> 
SELECT DISTINCT ?uri ?nombre ?uriPadre WHERE {
        ?uri a orgzar:organismo. 
        ?uri rdfs:label ?nombre.
        ?uri w3org:unitOf ?uriPadre.
}
# Lista completa de organismos, con toda su jerarquía asociada
PREFIX orgzar: <http://vocab.linkeddata.es/kos/sector-publico/>
PREFIX w3org: <http://www.w3.org/ns/org#>
SELECT DISTINCT ?nombre1 ?uriNivel2 ?nombre2 ?uriNivel3 ?nombre3 ?uriNivel4 ?nombre4 ?uriNivel5 ?nombre5 ?uriNivel6 ?nombre6 WHERE {
        <http://www.zaragoza.es/api/recurso/sector-publico/organismo/1> rdfs:label ?nombre1
        OPTIONAL{?uriNivel2 a orgzar:organismo.
          ?uriNivel2 rdfs:label ?nombre2.
          ?uriNivel2 w3org:unitOf <http://www.zaragoza.es/api/recurso/sector-publico/organismo/1>.
          ?uriNivel2 <http://datos.gob.es/voc/sector-publico/org#hierarchicalLevel> "2".
          OPTIONAL{?uriNivel3 a orgzar:organismo.
            ?uriNivel3 rdfs:label ?nombre3.
            ?uriNivel3 w3org:unitOf ?uriNivel2 .
            ?uriNivel3 <http://datos.gob.es/voc/sector-publico/org#hierarchicalLevel> "3".
            OPTIONAL{?uriNivel4 a orgzar:organismo.
              ?uriNivel4 rdfs:label ?nombre4.
              ?uriNivel4 w3org:unitOf ?uriNivel3 .
              ?uriNivel4 <http://datos.gob.es/voc/sector-publico/org#hierarchicalLevel> "4".
              OPTIONAL{?uriNivel5 a orgzar:organismo.
                ?uriNivel5 rdfs:label ?nombre5.
                ?uriNivel5 w3org:unitOf ?uriNivel4 .
                ?uriNivel5 <http://datos.gob.es/voc/sector-publico/org#hierarchicalLevel> "5".
                OPTIONAL{?uriNivel6 a orgzar:organismo.
                  ?uriNivel6 rdfs:label ?nombre6.
                  ?uriNivel6 w3org:unitOf ?uriNivel5 .
                  ?uriNivel6 <http://datos.gob.es/voc/sector-publico/org#hierarchicalLevel> "6".}}}}}
}
# Entidades de primer nivel, debe de ser nivel = 2 porque el nivel 1 es para describir al Ayuntamiento de Zaragoza
PREFIX orgzar: <http://vocab.linkeddata.es/kos/sector-publico/> 
PREFIX org: <http://datos.gob.es/voc/sector-publico/org#> 
SELECT DISTINCT str(?nombre) as ?nombre  WHERE {                        
        ?uri a orgzar:organismo. 
        ?uri rdfs:label ?nombre.
        ?uri org:hierarchicalLevel ?nivel
        FILTER(?nivel='2')
}
# Listar Servicios que dependen de la Dirección de Ciencia y Tecnología id=26009
PREFIX orgzar: <http://vocab.linkeddata.es/kos/sector-publico/> 
PREFIX w3org: <http://www.w3.org/ns/org#> 
SELECT DISTINCT ?uri str(?nombre) as ?nombre ?superior WHERE {
        ?uri a orgzar:organismo. 
       ?uri rdfs:label ?nombre.
      ?uri w3org:unitOf ?superior
        FILTER(?superior=<http://www.zaragoza.es/api/recurso/sector-publico/organismo/26009>)
}
# Listar Dependencias ubicadas en el Edificio Seminario (Via Hispanidad, 20)
PREFIX orgzar: <http://vocab.linkeddata.es/kos/sector-publico/> 
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#>
SELECT DISTINCT ?uri str(?nombre) as ?nombre str(?address) as ?address WHERE {
        ?uri a orgzar:organismo. 
        ?uri rdfs:label ?nombre.
        ?uri vcard:address ?addressInt.
                ?addressInt vcard:street-adr ?address.
        FILTER(?address="HISPANIDAD, VIA,20"^^xsd:string)
}
# Listar en detalle el Organismo con identificador 26009
PREFIX orgzar: <http://vocab.linkeddata.es/kos/sector-publico/> 
PREFIX w3org: <http://www.w3.org/ns/org#> 
PREFIX vcard: <http://www.w3.org/2006/vcard/ns#> 
SELECT DISTINCT ?uri str(?nombre) as ?nombre ?superior ?purpose str(?cp) as ?cp str(?address) as ?address ?fax ?email ?tel 
WHERE { 
        ?uri a orgzar:organismo. 
        ?uri dcterms:identifier '26009'. 
        ?uri rdfs:label ?nombre. 
        ?uri w3org:unitOf ?superior. 
        OPTIONAL {?uri w3org:purpose ?purpose.} 
        OPTIONAL {?uri vcard:hasPostalCode ?cp.} 
        OPTIONAL {?uri vcard:address/vcard:street-adr ?address.} 
        OPTIONAL {?uri vcard:faxNumber ?fax.} 
        OPTIONAL {?uri vcard:hasEmail ?email.} 
        OPTIONAL {?uri vcard:tel ?tel.} 
}
